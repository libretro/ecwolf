actor Trans 23
{
	radius 42
	points 5000
	health 850, 950, 1050, 1200
	speed 1, 3
	seesound "trans/sight"
	deathsound "trans/death"
	attacksound "trans/attack"
	dropitem "GoldKey"
	MONSTER
	+AMBUSH
	states
	{
		Spawn:
			TRNS A -1 NOP A_Look(0, 0, 0, 0, 360)
			stop
		See:
			TRNS A 5 NOP A_Chase
			TRNS A 1.5
			TRNS B 4 NOP A_Chase
			TRNS C 5 NOP A_Chase
			TRNS C 1.5
			TRNS D 4 NOP A_Chase
			loop
		Missile:
			TRNS E 15 A_FaceTarget
			TRNS F 5 bright A_FaceTarget
			TRNS GFGFGE 5 bright A_WolfAttack
			goto See
		Death:
			TRNS A .5 A_Fall
			TRNS A 52.5 A_DeathScream
			TRNS HIJ 7.5
			TRNS K -1
			stop
	}
}

actor UberMutant 24
{
	radius 42
	points 5000
	health 1050, 1150, 1250, 1400
	speed 1, 5.859
	deathsound "uber/death"
	attacksound "uber/attack"
	dropitem "GoldKey"
	MONSTER
	+AMBUSH
	states
	{
		Spawn:
			UBER A -1 A_Look(0, 0, 0, 0, 360)
			stop
		See:
			UBER A 5 NOP A_Chase
			UBER A 1.5
			UBER B 4 NOP A_Chase
			UBER C 5 NOP A_Chase
			UBER C 1.5
			UBER D 4 NOP A_Chase
			loop
		Missile:
			UBER E 15 A_FaceTarget
			UBER F 6 bright A_FaceTarget
			UBER G 0 bright A_MeleeAttack(10, 1.0)
			UBER G 6 bright A_WolfAttack
			UBER H 0 bright A_MeleeAttack(10, 1.0)
			UBER H 6 bright A_WolfAttack
			UBER G 0 bright A_MeleeAttack(10, 1.0)
			UBER G 6 bright A_WolfAttack
			UBER F 0 bright A_MeleeAttack(10, 1.0)
			UBER F 6 bright A_WolfAttack
			UBER E 0 A_MeleeAttack(10, 1.0)
			UBER E 6 A_WolfAttack
			goto See
		Death:
			UBER A .5 A_Fall
			UBER A 35 A_DeathScream
			UBER HIJK 7.5
			UBER L -1
			stop
	}
}

actor Wilhelm 25
{
	radius 42
	points 5000
	health 950, 1050, 1150, 1300
	speed 1, 4
	missilechance 8
	seesound "will/sight"
	deathsound "will/death"
	attacksound "will/attack"
	dropitem "GoldKey"
	MONSTER
	+AMBUSH
	states
	{
		Spawn:
			WILL A -1 NOP A_Look(0, 0, 0, 0, 360)
			stop
		See:
			WILL A 5 NOP A_Chase("*", "*", CHF_BACKOFF)
			WILL A 1.5
			WILL B 4 NOP A_Chase("*", "*", CHF_BACKOFF)
			WILL C 5 NOP A_Chase("*", "*", CHF_BACKOFF)
			WILL C 1.5
			WILL D 4 NOP A_Chase("*", "*", CHF_BACKOFF)
			loop
		Missile:
			WILL E 15 A_FaceTarget
			WILL F 5 bright A_FaceTarget
			WILL G 0 bright A_FaceTarget
			WILL G 5 bright A_CustomMissile("Rocket")
			WILL HGH 5 bright A_WolfAttack
			WILL H 0 bright A_WolfAttack
			goto See
		Death:
			WILL A .5 A_Fall
			WILL A 35 A_DeathScream
			WILL IJK 5
			WILL L -1
			stop
	}
}

actor DeathKnight 26
{
	radius 42
	points 5000
	health 1250, 1350, 1450, 1600
	speed 1, 4
	missilechance 8
	seesound "knight/sight"
	deathsound "knight/death"
	attacksound "knight/attack"
	dropitem "GoldKey"
	MONSTER
	+AMBUSH
	states
	{
		Spawn:
			DKNT A -1 NOP A_Look(0, 0, 0, 0, 360)
			stop
		See:
			DKNT A 5 NOP A_Chase("*", "*", CHF_BACKOFF)
			DKNT A 1.5
			DKNT B 4 NOP A_Chase("*", "*", CHF_BACKOFF)
			DKNT C 5 NOP A_Chase("*", "*", CHF_BACKOFF)
			DKNT C 1.5
			DKNT D 4 NOP A_Chase("*", "*", CHF_BACKOFF)
			loop
		Missile:
			DKNT E 15 A_FaceTarget
			DKNT F 5 bright A_FaceTarget
			DKNT H 0 bright A_WolfAttack
			DKNT H 5 bright A_CustomMissile("HighRocket", 32, 0, 4)
			DKNT G 5 bright A_WolfAttack
			DKNT H 0 bright A_WolfAttack
			DKNT H 5 bright A_CustomMissile("HighRocket", 32, 0, -4)
			DKNT H 0 bright A_WolfAttack
			goto See
		Death:
			DKNT A .5 A_Fall
			DKNT A 52.5 A_DeathScream
			DKNT IJKLMN 5
			DKNT O -1
			stop
	}
}
