actor WolfWeapon : Weapon
{
	weapon.yadjust 16
	states
	{
		Select:
			TNT1 A 0 A_Raise
			loop
		Deselect:
			TNT1 A 0 A_Lower
			loop
	}
}

actor Knife : WolfWeapon
{
	attacksound "weapon/knife/attack"
	states
	{
		Ready:
			KNIF A 1 A_WeaponReady
			loop
		Fire:
			KNIF B 3
			KNIF C 3 A_CustomPunch(1, false, CPF_ALWAYSPLAYSOUND)
			KNIF DE 3
			goto Ready
	}
}

actor Pistol : WolfWeapon
{
	attacksound "weapon/pistol/attack"
	states
	{
		Ready:
			PISG A 1 A_WeaponReady
			loop
		Fire:
			PISG B 3
			PISG C 3 A_GunAttack
			PISG DE 3
			goto Ready
	}
}

actor MachineGun : WolfWeapon 60
{
	attacksound "weapon/machine/attack"
	inventory.pickupsound "weapon/machine/pickup"
	states
	{
		Spawn:
			MGUN A -1
			stop
		Ready:
			MCHG A 1 A_WeaponReady
			loop
		Fire:
			MCHG B 3
		Hold:
			MCHG C 3 A_GunAttack
			MCHG D 3
			MCHG E 3 A_ReFire
			goto Ready
	}
}

actor GatlingGun : WolfWeapon 61
{
	attacksound "weapon/gatling/attack"
	inventory.pickupsound "weapon/gatling/pickup"
	states
	{
		Spawn:
			CGUN A -1
			stop
		Ready:
			CHGG A 1 A_WeaponReady
			loop
		Fire:
			CHGG B 3
		Hold:
			CHGG CD 3 A_GunAttack
			CHGG E 3 A_ReFire
			goto Ready
	}
}
