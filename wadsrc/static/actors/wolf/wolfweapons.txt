actor WolfWeapon : Weapon
{
	weapon.ammotype1 "Clip"
	weapon.ammouse1 1
	weapon.ammogive1 6
	weapon.yadjust 19
	+WEAPON.NOGRIN
	states
	{
		Select:
			TNT1 A 0 A_Raise
			loop
		Deselect:
			TNT1 A 0 A_Lower
			loop
	}
}

actor Knife : WolfWeapon
{
	inventory.icon "KNIFE"
	weapon.ammouse1 0
	weapon.selectionorder 3700
	attacksound "weapon/knife/attack"
	states
	{
		Ready:
			KNIF A 1 A_WeaponReady
			loop
		Fire:
			KNIF B 3
			KNIF C 3 A_CustomPunch(1, false, CPF_ALWAYSPLAYSOUND)
			KNIF DE 3
			goto Ready
	}
}

actor Pistol : WolfWeapon
{
	inventory.icon "PISTOL"
	weapon.selectionorder 1900
	attacksound "weapon/pistol/attack"
	states
	{
		Ready:
			PISG A 1 A_WeaponReady
			loop
		Fire:
			PISG B 3
			PISG C 3 bright A_GunAttack
			PISG DE 3
			goto Ready
	}
}

actor MachineGun : WolfWeapon 60
{
	attacksound "weapon/machine/attack"
	inventory.icon "MACHGUN"
	inventory.pickupsound "weapon/machine/pickup"
	weapon.selectionorder 1300
	states
	{
		Spawn:
			MGUN A -1
			stop
		Ready:
			MCHG A 1 A_WeaponReady
			loop
		Fire:
			MCHG B 3
		Hold:
			MCHG C 3 bright A_GunAttack
			MCHG D 3
			MCHG E 3 A_ReFire
			goto Ready
	}
}

actor GatlingGun : WolfWeapon 61
{
	attacksound "weapon/gatling/attack"
	inventory.icon "GATLGUN"
	inventory.pickupsound "weapon/gatling/pickup"
	weapon.selectionorder 700
	-WEAPON.NOGRIN
	states
	{
		Spawn:
			CGUN A -1
			stop
		Ready:
			CHGG A 1 A_WeaponReady
			loop
		Fire:
			CHGG B 3
		Hold:
			CHGG CD 3 bright A_GunAttack
			CHGG E 3 A_ReFire
			goto Ready
	}
}
