actor Guard 11
{
	radius 42
	points 100
	health 25
	speed 1, 3
	sighttime 1, 4
	dropitem "Clip", 4
	attacksound "guard/attack"
	deathsound "guard/death"
	secretdeathsound "guard/fart"
	seesound "guard/sight"
	MONSTER
	states
	{
		Spawn:
			GARD A -1 NOP A_Look
			stop
		Path:
			GARD B 10 NOP A_Chase
			GARD B 2.5
			GARD C 7.5 NOP A_Chase
			GARD D 10 NOP A_Chase
			GARD D 2.5
			GARD E 7.5 NOP A_Chase
			loop
		See:
			GARD B 5 NOP A_Chase
			GARD B 1.5
			GARD C 4 NOP A_Chase
			GARD D 5 NOP A_Chase
			GARD D 1.5
			GARD E 4 NOP A_Chase
			loop
		Missile:
			GARD FG 10 A_FaceTarget
			GARD H 10 bright A_WolfAttack
			goto See
		Pain:
			GARD I 5
			goto See
			GARD J 5
			goto See
		Death:
			GARD K 7.5 A_Fall
			GARD L 7.5 A_DeathScream
			GARD M 7.5
			GARD N -1
			stop
	}
}

actor DeadGuard 81
{
	states
	{
		Spawn:
			GARD N -1
			stop
	}
}

actor Dog 14
{
	radius 42
	points 200
	health 1
	speed 2.93, 5.859
	sighttime 1, 8
	attacksound "dog/attack"
	deathsound "dog/death"
	secretdeathsound "dog/fart"
	seesound "dog/sight"
	MONSTER
	-CANUSEWALLS
	states
	{
		Spawn:
		Path:
			DOGY A 10 NOP A_Chase
			DOGY A 2.5
			DOGY B 7.5 NOP A_Chase
			DOGY C 10 NOP A_Chase
			DOGY C 2.5
			DOGY D 7.5 NOP A_Chase
			loop
		Melee:
			DOGY EF 5 A_FaceTarget
			DOGY G 5 A_MeleeAttack(random(1,15))
			DOGY EA 5
			goto See
		See:
			DOGY A 5 NOP A_Chase
			DOGY A 1.5
			DOGY B 4 NOP A_Chase
			DOGY C 5 NOP A_Chase
			DOGY C 1.5
			DOGY D 4 NOP A_Chase
			loop
		Death:
			DOGY H 7.5 A_Fall
			DOGY I 7.5 A_DeathScream
			DOGY J 7.5
			DOGY K -1
			stop
	}
}

actor Officer 12
{
	radius 42
	points 400
	health 50
	speed 1, 5
	sighttime 2
	dropitem "Clip", 4
	attacksound "officer/attack"
	deathsound "officer/death"
	secretdeathsound "officer/fart"
	seesound "officer/sight"
	MONSTER
	states
	{
		Spawn:
			OFFI A -1 NOP A_Look
			stop
		Path:
			OFFI B 10 NOP A_Chase
			OFFI B 2.5
			OFFI C 7.5 NOP A_Chase
			OFFI D 10 NOP A_Chase
			OFFI D 2.5
			OFFI E 7.5 NOP A_Chase
			loop
		Pain:
			OFFI I 5
			goto See
			OFFI J 5
			goto See
		Missile:
			OFFI F 3 A_FaceTarget
			OFFI G 10 A_FaceTarget
			OFFI H 5 bright A_WolfAttack
			goto See
		See:
			OFFI B 5 NOP A_Chase
			OFFI B 1.5
			OFFI C 4 NOP A_Chase
			OFFI D 5 NOP A_Chase
			OFFI D 1.5
			OFFI E 4 NOP A_Chase
			loop
		Death:
			OFFI K 5.5 A_Fall
			OFFI L 5.5 A_DeathScream
			OFFI MN 5.5
			OFFI O -1
			stop
	}
}

actor Mutant 15
{
	radius 42
	points 700
	health 45, 55, 55, 65
	speed 1, 3
	sighttime 1, 6
	dropitem "Clip", 4
	attacksound "mutant/attack"
	deathsound "mutant/death"
	secretdeathsound "mutant/fart"
	MONSTER
	states
	{
		Spawn:
			MTNT A -1 NOP A_Look
			stop
		Path:
			MTNT B 10 NOP A_Chase
			MTNT B 2.5
			MTNT C 7.5 NOP A_Chase
			MTNT D 10 NOP A_Chase
			MTNT D 2.5
			MTNT E 7.5 NOP A_Chase
			loop
		Pain:
			MTNT J 5
			goto See
			MTNT K 5
			goto See
		See:
			MTNT B 5 NOP A_Chase
			MTNT B 1.5
			MTNT C 4 NOP A_Chase
			MTNT D 5 NOP A_Chase
			MTNT D 1.5
			MTNT E 4 NOP A_Chase
			loop
		Missile:
			MTNT F 3 A_FaceTarget
			MTNT G 10 bright A_WolfAttack
			MTNT H 5 A_FaceTarget
			MTNT I 10 bright A_WolfAttack
			goto See
		Death:
			MTNT L 3.5 A_Fall
			MTNT M 3.5 A_DeathScream
			MTNT MO 3.5
			MTNT P -1
			stop
	}
}

actor WolfensteinSS 13 // ZDoom compatible name
{
	radius 42
	points 500
	health 100
	speed 1, 4
	sighttime 1, 6
	dropitem "MachineGun"
	dropitem "Clip", 4
	attacksound "wolfss/attack"
	deathsound "wolfss/death"
	secretdeathsound "wolfss/fart"
	seesound "wolfss/sight"
	MONSTER
	+DROPBASEDONTARGET
	states
	{
		Spawn:
			SSWV W -1 NOP A_Look
			stop
		Path:
			SSWV A 10 NOP A_Chase
			SSWV A 2.5
			SSWV B 7.5 NOP A_Chase
			SSWV C 10 NOP A_Chase
			SSWV C 2.5
			SSWV D 7.5 NOP A_Chase
			loop
		See:
			SSWV A 5 NOP A_Chase
			SSWV A 1.5
			SSWV B 4 NOP A_Chase
			SSWV C 5 NOP A_Chase
			SSWV C 1.5
			SSWV D 4 NOP A_Chase
			loop
		Pain:
			SSWV H 5
			goto See
		Pain2:
			SSWV I 5
			goto See
		Missile:
			SSWV EF 10 A_FaceTarget
			SSWV G 5 bright A_WolfAttack(0, "*", 0.667)
			SSWV F 5 A_FaceTarget
			SSWV G 5 bright A_WolfAttack(0, "*", 0.667)
			SSWV F 5 A_FaceTarget
			SSWV G 5 bright A_WolfAttack(0, "*", 0.667)
			SSWV F 5 A_FaceTarget
			SSWV G 5 bright A_WolfAttack(0, "*", 0.667)
			goto See
		Death:
			SSWV J 7.5 A_Fall
			SSWV K 7.5 A_DeathScream
			SSWV L 7.5
			SSWV M -1
			stop
	}
}
