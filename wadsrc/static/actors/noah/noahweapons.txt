// Unfortunately it looks like none of the weapons have official names :(

actor HandFeed : WolfWeapon
{
	weapon.ammotype1 "Feed"
	weapon.ammouse1 0
	weapon.selectionorder 3700
	attacksound "weapon/knife/attack"
	+WEAPON.NOALERT
	states
	{
		Ready:
			HAND A 1 A_WeaponReady
			loop
		Fire:
			HAND BC 3
			HAND D 3 A_CustomPunch(random(0,3), true, CPF_ALWAYSPLAYSOUND) //Damage: US_RndT()&3
			HAND E 3
			// In Noah's Ark weapons automatically refired, but it took at least one tic
			HAND A 1
			goto Ready
	}
}

actor Slingshot : WolfWeapon
{
	weapon.ammotype1 "Feed"
	weapon.selectionorder 1900
	attacksound "weapon/sling/attack"
	states
	{
		Ready:
			SLNG A 1 A_WeaponReady
			loop
		Fire:
			SLNG B 3
			SLNG C 3 A_GunAttack(GAF_MACDAMAGE|GAF_NORANDOM, "*", 1.0, random(0,0xF))
			SLNG DE 3
			SLNG A 1
			goto Ready
	}
}

actor SuperSlingshot : WolfWeapon 181
{
	attacksound "weapon/supersling/attack"
	weapon.ammotype1 "Feed"
	weapon.selectionorder 1300
	overheadicon "TILE8:7E"
	+PLOTONAUTOMAP
	+INVENTORY.ALWAYSPICKUP
	states
	{
		Spawn:
			SSGG A -1
			stop
		Ready:
			SSLG A 1 A_WeaponReady
			loop
		Fire:
			SSLG B 3
		Hold:
			SSLG C 3
			SSLG D 3 A_GunAttack(GAF_MACDAMAGE|GAF_NORANDOM, "*", 1.0, random(0,0xF))
			SSLG E 3 A_ReFire
			goto Ready
	}
}

actor AutoSlingshot : WolfWeapon 182
{
	attacksound "weapon/autosling/attack"
	weapon.ammotype1 "Feed"
	weapon.selectionorder 700
	overheadicon "TILE8:7E"
	+PLOTONAUTOMAP
	-WEAPON.NOGRIN
	+WEAPON.ALWAYSGRIN
	+INVENTORY.ALWAYSPICKUP
	states
	{
		Spawn:
			ASGG A -1
			stop
		Ready:
			ASLG A 1 A_WeaponReady
			loop
		Fire:
			ASLG B 3
		Hold:
			ASLG C 3
			ASLG D 3 A_GunAttack(GAF_MACDAMAGE|GAF_NORANDOM, "*", 1.0, random(0,0xF))
			ASLG D 0 A_GunAttack(GAF_MACDAMAGE|GAF_NORANDOM, "*", 1.0, random(0,0xF))
			ASLG E 3 A_ReFire
			goto Ready
	}
}

actor Cantaloupe
{
	radius 11
	speed 24
	damage (random[MissileDamage](1, 16))
	seesound "cantaloupe/fire"
	deathsound "cantaloupe/hit"
	PROJECTILE
	+RIPPER
	states
	{
		Spawn:
			CTLP A 1.5
			loop
		Death:
			// The state table has three identical states with a duration of 6
			// (3 in DECORATE terms), but the code sets the first state to 1
			// (0.5 in DECORATE terms).
			CTLP B 6.5
			stop
	}
}

actor CantaloupeSlingshot : WolfWeapon 190
{
	weapon.ammotype1 "CantaloupeAmmo"
	weapon.ammogive1 20
	overheadicon "TILE8:7E"
	+PLOTONAUTOMAP
	+INVENTORY.ALWAYSPICKUP
	+WEAPON.NOAUTOFIRE
	states
	{
		Spawn:
			CSGG A -1
			stop
		Ready:
			CSLG A 1 A_WeaponReady
			loop
		Fire:
			CSLG BC 3.5
			CSLG D 3.5 A_FireCustomMissile("Cantaloupe")
			CSLG E 3.5 A_FireCustomMissile("Cantaloupe")
			goto Ready
	}
}

actor Watermelon
{
	radius 11
	speed 24
	damage (random[MissileDamage](0, 3)<<4)
	seesound "watermelon/fire"
	deathsound "watermelon/hit"
	PROJECTILE
	+RIPPER
	states
	{
		Spawn:
			WMLN A 1.5
			loop
		Death:
			WMLN B 6.5
			stop
	}
}

actor MelonSlingshot : WolfWeapon 192
{
	weapon.ammotype1 "MelonAmmo"
	weapon.ammogive1 5
	overheadicon "TILE8:7E"
	+PLOTONAUTOMAP
	+INVENTORY.ALWAYSPICKUP
	+WEAPON.NOAUTOFIRE
	states
	{
		Spawn:
			MSGG A -1
			stop
		Ready:
			MSLG A 1 A_WeaponReady
			loop
		Fire:
			MSLG BCD 3
			MSLG E 3 A_FireCustomMissile("Watermelon")
			goto Ready
	}
}
